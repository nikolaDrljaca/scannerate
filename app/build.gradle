plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id 'org.jetbrains.kotlin.android'
}

sourceSets {
    main {
        java {
            srcDirs += 'build/generated/source/kapt/debug/com/drbrosdev/studytextscan/ui/home'
            srcDirs += 'build/generated/source/kapt/debug/com/drbrosdev/studytextscan/ui/detailscan'
            srcDirs += 'build/generated/source/kapt/debug/com/drbrosdev/studytextscan/ui/licenses'
        }
    }
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }
    compileSdk 34
//    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.drbrosdev.studytextscan"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 13
        versionName "1.8"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            // Enables code shrinking, obfuscation, and optimization for only
            // your project's release build type.
            minifyEnabled false
            // Enables resource shrinking, which is performed by the
            // Android Gradle plugin.
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        viewBinding true
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = "1.4.0"
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
    namespace 'com.drbrosdev.studytextscan'
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    implementation 'androidx.activity:activity-ktx:1.7.0-alpha01'
    implementation "io.coil-kt:coil:2.2.1"

    // To recognize Latin script
    implementation 'com.google.mlkit:text-recognition:16.0.0-beta4'
    // To recognize Chinese script
    implementation 'com.google.mlkit:text-recognition-chinese:16.0.0-beta4'
    // To recognize Devanagari script
    implementation 'com.google.mlkit:text-recognition-devanagari:16.0.0-beta4'
    // To recognize Japanese script
    implementation 'com.google.mlkit:text-recognition-japanese:16.0.0-beta4'
    // To recognize Korean script
    implementation 'com.google.mlkit:text-recognition-korean:16.0.0-beta4'

    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.0-alpha02'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    //navigation
    def nav_version = "2.5.2"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    //to fix Pending Intent flag, we dont even use workManager, yaaay android
    implementation 'androidx.work:work-runtime-ktx:2.7.1'

    //lottie animations
    def lottie_version = "5.2.0"
    implementation "com.airbnb.android:lottie:$lottie_version"

    //jetpack preferences datastore (sharedPrefs)
    implementation 'androidx.datastore:datastore-preferences:1.0.0'
    implementation 'androidx.datastore:datastore-core:1.0.0'

    //room database
    def room_version = "2.5.0"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    // optional - Kotlin Extensions and Coroutines support for Room
    implementation "androidx.room:room-ktx:$room_version"

    // Koin main features for Android (Scope,ViewModel ...)
    implementation "io.insert-koin:koin-android:3.3.2"
    // Koin core features
    implementation "io.insert-koin:koin-core:3.3.2"

    //epoxy
    def epoxyVersion = "4.6.3"
    implementation "com.airbnb.android:epoxy:$epoxyVersion"
    // Add the annotation processor if you are using Epoxy's annotations (recommended)
    kapt "com.airbnb.android:epoxy-processor:$epoxyVersion"

    //entity extraction - text recognition
    implementation 'com.google.mlkit:entity-extraction:16.0.0-beta4'

    //Google ML-kit Task extensions
    //Source: https://github.com/Kotlin/kotlinx.coroutines/tree/master/integration/kotlinx-coroutines-play-services
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.6.4'

    //image cropper
    implementation 'com.vanniktech:android-image-cropper:4.5.0'

    //COMPOSE
    // When using a MDC theme
    //compose BOM for proper versions
    api(platform("dev.chrisbanes.compose:compose-bom:2023.02.00-alpha01"))
    implementation 'androidx.compose.material:material'
    implementation 'androidx.compose.foundation:foundation:1.4.0-alpha05'
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    debugImplementation 'androidx.compose.ui:ui-tooling'

    implementation 'androidx.activity:activity-compose:1.6.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-compose:2.6.0-alpha04'
    implementation "br.com.devsrsouza.compose.icons.android:line-awesome:1.0.0"

    //billing
//    def billing_version = "5.0.0"
//    implementation "com.android.billingclient:billing-ktx:$billing_version"

    //in-app review
    implementation 'com.google.android.play:review-ktx:2.0.1'

    //Icons
    implementation "br.com.devsrsouza.compose.icons.android:tabler-icons:1.0.0"
    //google fonts
    implementation "androidx.compose.ui:ui-text-google-fonts:1.3.3"
}
